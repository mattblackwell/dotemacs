#+TITLE: DotEmacs
#+AUTHOR: Mathieu Marques

* Table of Content                                                      :TOC:
- [[#bootstrap][Bootstrap]]
  - [[#use-better-defaults][Use better defaults]]
  - [[#load-customel][Load =.custom.el=]]
  - [[#load-secretel][Load =.secret.el=]]
- [[#theme][Theme]]
- [[#languages][Languages]]
  - [[#css][CSS]]
  - [[#docker][Docker]]
  - [[#html][HTML]]
  - [[#javascript][JavaScript]]
  - [[#lisp][Lisp]]
  - [[#markdown][Markdown]]
  - [[#org][Org]]
  - [[#python][Python]]
  - [[#yaml][YAML]]
- [[#features][Features]]
  - [[#alert][Alert]]
  - [[#auto-completion][Auto-Completion]]
  - [[#comments][Comments]]
  - [[#customization-menus][Customization Menus]]
  - [[#dictionary][Dictionary]]
  - [[#diff][Diff]]
  - [[#dired][Dired]]
  - [[#expand][Expand]]
  - [[#exwm][EXWM]]
  - [[#folding][Folding]]
  - [[#help][Help]]
  - [[#hydra][Hydra]]
  - [[#irc][IRC]]
  - [[#ivy-counsel-and-swiper][Ivy, Counsel and Swiper]]
  - [[#linters][Linters]]
  - [[#mode-line][Mode-Line]]
  - [[#navigation][Navigation]]
  - [[#os-specific][OS-Specific]]
  - [[#parentheses][Parentheses]]
  - [[#paste][Paste]]
  - [[#point-and-region][Point and Region]]
  - [[#projectile][Projectile]]
  - [[#quality-of-life][Quality of Life]]
  - [[#slack][Slack]]
  - [[#version-control][Version Control]]
  - [[#whitespaces][Whitespaces]]
  - [[#windows][Windows]]

* Bootstrap

** Use better defaults

Maximize by default, fullscreen on Windows.

#+BEGIN_SRC emacs-lisp
(set-frame-parameter nil 'fullscreen (if (eq system-type 'windows-nt)
                                         'fullboth 'maximized))
#+END_SRC

Some UI elements are rather invasive. No-mouse master race.

#+BEGIN_SRC emacs-lisp
(when window-system
  (blink-cursor-mode 0)                           ; Disable the cursor blinking
  (scroll-bar-mode 0)                             ; Disable the scroll bar
  (tool-bar-mode 0)                               ; Disable the tool bar
  (tooltip-mode 0))                               ; Disable the tooltips
#+END_SRC

Here are what I consider better defaults as per my own experience.

#+BEGIN_SRC emacs-lisp
(setq-default
 ad-redefinition-action 'accept                   ; Silence warnings for redefinition
 confirm-kill-emacs 'yes-or-no-p                  ; Confirm before exiting Emacs
 cursor-in-non-selected-windows t                 ; Hide the cursor in inactive windows
 delete-by-moving-to-trash t                      ; Delete files to trash
 display-time-default-load-average nil            ; Don't display load average
 display-time-format "%H:%M"                      ; Format the time string
 fill-column 80                                   ; Set width for automatic line breaks
 help-window-select t                             ; Focus new help windows when opened
 indent-tabs-mode nil                             ; Stop using tabs to indent
 inhibit-startup-screen t                         ; Disable start-up screen
 initial-scratch-message ""                       ; Empty the initial *scratch* buffer
 left-margin-width 1 right-margin-width 1         ; Add left and right margins
 mode-require-final-newline 'visit                ; Add a newline at EOF on visit
 mouse-yank-at-point t                            ; Yank at point rather than pointer
 ns-use-srgb-colorspace nil                       ; Don't use sRGB colors
 recenter-positions '(5 top bottom)               ; Set re-centering positions
 scroll-conservatively most-positive-fixnum       ; Always scroll by one line
 select-enable-clipboard t                        ; Merge system's and Emacs' clipboard
 show-trailing-whitespace nil                     ; Display trailing whitespaces
 split-height-threshold nil                       ; Disable vertical window splitting
 split-width-threshold nil                        ; Disable horizontal window splitting
 tab-width 4                                      ; Set width for tabs
 uniquify-buffer-name-style 'forward              ; Uniquify buffer names
 window-combination-resize t                      ; Resize windows proportionally
 x-stretch-cursor t)                              ; Stretch cursor to the glyph width
(display-time-mode 1)                             ; Enable time in the mode-line
(fringe-mode 0)                                   ; Hide fringes
(fset 'yes-or-no-p 'y-or-n-p)                     ; Replace yes/no prompts with y/n
(global-hl-line-mode 1)                           ; Hightlight current line
(global-subword-mode 1)                           ; Iterate through CamelCase words
(menu-bar-mode 0)                                 ; Disable the menu bar
(mouse-avoidance-mode 'banish)                    ; Avoid collision of mouse with point
(put 'downcase-region 'disabled nil)              ; Enable downcase-region
(put 'upcase-region 'disabled nil)                ; Enable upcase-region
#+END_SRC

Garbage-collect on focus-out, Emacs /should/ feel snappier.

#+BEGIN_SRC emacs-lisp
(add-hook 'focus-out-hook #'garbage-collect)
#+END_SRC

** Load =.custom.el=

One is able to use the customization interface that is bundled within Emacs. It is
meant to help people who are not familiar with Emacs Lisp in the configuration of
Emacs itself. By default, changes in the customization will be automatically detected
and appended at the end of the configuration file, =init.el=.

Since that in my case, the actual configuration file is a new one, crafted by
=org-mode=, adding code at the end of =init.el= might mess things up. The following
tells Emacs to add extra code in another file that would be then loaded, if existing.

#+BEGIN_SRC emacs-lisp
(setq-default custom-file (expand-file-name ".custom.el" user-emacs-directory))
(when (file-exists-p custom-file)
  (load custom-file))
#+END_SRC

** Load =.secret.el=

I load =~/.emacs.d/.secret.el= to keep sensible things out of version control. For
instance, you could set your identity by customizing both =user-full-name= and
=user-mail-address=. This is also where you want your API tokens to live.

#+BEGIN_SRC emacs-lisp
(defvar me/erc-nick               nil       "The ERC nick to use.")
(defvar me/erc-password           nil       "The ERC password to use.")
(defvar me/erc-port               nil       "The ERC port to use.")
(defvar me/erc-server             nil       "The ERC server to use.")
(defvar me/font-family            "Monaco"  "The font to use.")
(defvar me/font-size-default      120       "The font size to use for default text.")
(defvar me/font-size-header-line  130       "The font size to use for the header-line.")
(defvar me/font-size-mode-line    120       "The font size to use for the mode-line.")
(defvar me/font-size-title        150       "The font size to use for titles.")

(let ((secret.el (expand-file-name ".secret.el" user-emacs-directory)))
  (when (file-exists-p secret.el)
    (load secret.el)))
#+END_SRC

* Theme

Load =zenburn-theme= and fix some high-level faces to match my personal preferences.

- *FIXME*: The un-bold'ify thing runs too early.

#+BEGIN_SRC emacs-lisp
(use-package zenburn-theme
  :demand t
  :config
  (set-face-attribute 'bold nil :weight 'normal)
  (load-theme 'zenburn t)

  ;; Customize Zenburn implementation
  (set-face-attribute 'default nil :height me/font-size-default :font me/font-family)
  (set-face-attribute 'font-lock-comment-face nil :italic t)
  (set-face-attribute 'font-lock-doc-face nil :italic t)
  (zenburn-with-color-variables
    (set-face-attribute 'button nil :foreground zenburn-yellow-2)
    (set-face-attribute 'help-argument-name nil :foreground zenburn-orange :italic nil)
    (set-face-attribute 'hl-line nil :background zenburn-bg+1)
    (set-face-attribute 'header-line nil
                        :background zenburn-bg-1
                        :box `(:line-width 2 :color ,zenburn-bg-1)
                        :height me/font-size-header-line)
    (set-face-attribute 'isearch nil
                        :background zenburn-blue
                        :foreground zenburn-blue-5)
    (set-face-attribute 'isearch-lazy-highlight-face nil
                        :foreground zenburn-blue
                        :background zenburn-blue-5)
    (set-face-attribute 'region nil
                        :background zenburn-fg-1
                        :distant-foreground 'unspecified)
    (set-face-attribute 'vertical-border nil :foreground zenburn-bg))

  ;; Un-bold'ify all the things
  (mapc
   (lambda (face)
     (when (eq (face-attribute face :weight) 'bold)
       (set-face-attribute face nil :weight 'normal)))
   (face-list)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package mdi
  :ensure nil
  :load-path "lisp/mdi/"
  :after zenburn-theme)
#+END_SRC

* Languages

As pedantic as I like to be, some packages have a =use-package= clause for nothing
but the customization of its lighter.

- *TODO*: Replace all old defadvice's.

** CSS

#+BEGIN_SRC emacs-lisp
(use-package css-mode
  :ensure nil
  :delight css-mode "CSS"
  :config (setq-default css-indent-offset 2))

(use-package scss-mode
  :preface
  (defun me/scss-set-comment-style ()
    (setq-local comment-end "")
    (setq-local comment-start "//"))
  :delight scss-mode "SCSS"
  :mode ("\\.css\\'" "\\.sass\\'" "\\.scss\\'")
  :init (add-hook 'scss-mode-hook #'me/scss-set-comment-style))
#+END_SRC

** Docker

#+BEGIN_SRC emacs-lisp
(use-package dockerfile-mode
  :delight dockerfile-mode "Dockerfile"
  :mode "Dockerfile\\'")
#+END_SRC

** HTML

HTML mode is defined in =sgml-mode.el=.

#+BEGIN_SRC emacs-lisp
(use-package sgml-mode
  :ensure nil
  :delight html-mode "HTML"
  :init
  (add-hook 'html-mode-hook #'sgml-electric-tag-pair-mode)
  (add-hook 'html-mode-hook #'sgml-name-8bit-mode)
  :config (setq-default sgml-basic-offset 2))
#+END_SRC

** JavaScript

Tern is a code-analysis engine for JavaScript. I use it to check syntax and grammar
in my JavaScript code. With an auto-complete frontend, it can also provide candidates
for variables and properties.

- *FIXME*: Indent level is broken
           (https://github.com/joshwnj/json-mode/issues/32).

#+BEGIN_SRC emacs-lisp
(use-package js
  :ensure nil
  :delight js-mode "JavaScript"
  :config (setq-default js-indent-level 2))

(use-package json-mode
  :delight json-mode "JSON"
  :mode ("\\.jshintrc\\'" "\\.json\\'")
  :preface
  (defun me/json-set-indent-level ()
    (setq-local js-indent-level 2))
  :init (add-hook 'json-mode-hook #'me/json-set-indent-level))

(use-package tern
  :init (add-hook 'js-mode-hook #'tern-mode))
#+END_SRC

** Lisp

#+BEGIN_SRC emacs-lisp
(use-package lisp-mode
  :ensure nil
  :delight lisp-mode "Lisp"
  :config
  (delight
   '((emacs-lisp-mode "Emacs Lisp")
     (lisp-interaction-mode "Lisp Interaction"))))
#+END_SRC

** Markdown

#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :delight markdown-mode "Markdown"
  :mode
  ("INSTALL\\'"
   "CONTRIBUTORS\\'"
   "LICENSE\\'"
   "README\\'"
   "\\.markdown\\'"
   "\\.md\\'")
  :config
  (unbind-key "M-<down>" markdown-mode-map)
  (unbind-key "M-<up>" markdown-mode-map)
  (setq-default markdown-asymmetric-header t))
#+END_SRC

** Org

This very file is organized with =org-mode=. I am definitely not a power user of Org,
but I'm getting there. :-)

#+BEGIN_QUOTE
Org mode is for keeping notes, maintaining TODO lists, planning projects, and
authoring documents with a fast and effective plain-text system.

[[http://orgmode.org/][Carsten Dominik]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package org
  :ensure nil
  :delight org-mode "Org"
  :preface
  (defun me/org-src-buffer-name (org-buffer-name language)
    "Construct the buffer name for a source editing buffer. See
`org-src--construct-edit-buffer-name'."
    (format "*%s*" org-buffer-name))
  (defun me/org-backward-paragraph-shifted ()
    "See `org-backward-paragraph'. Support shift."
    (interactive "^")
    (org-backward-paragraph))
  (defun me/org-forward-paragraph-shifted ()
    "See `org-forward-paragraph'. Support shift."
    (interactive "^")
    (org-forward-paragraph))
  :bind
  (:map org-mode-map
        ([remap backward-paragraph] . me/org-backward-paragraph-shifted)
        ([remap forward-paragraph] . me/org-forward-paragraph-shifted)
        ("<M-return>" . org-insert-heading-after-current))
  :init
  (add-hook 'org-mode-hook #'org-sticky-header-mode)
  (add-hook 'org-mode-hook #'toc-org-enable)
  :config
  (setq-default
   org-descriptive-links nil
   org-support-shift-select 'always
   org-startup-folded nil
   org-startup-truncated nil)
  (zenburn-with-color-variables
    (set-face-attribute 'org-block nil :background zenburn-bg+05))
  (unbind-key "<C-return>" org-mode-map)
  (unbind-key "<C-S-down>" org-mode-map)
  (unbind-key "<C-S-up>" org-mode-map)
  (advice-add 'org-src--construct-edit-buffer-name :override #'me/org-src-buffer-name))

(use-package org-src
  :ensure nil
  :after org
  :config
  (setq-default
   org-edit-src-content-indentation 0
   org-edit-src-persistent-message nil
   org-src-window-setup 'current-window))
#+END_SRC

Display the current Org header in the header-line.

#+BEGIN_SRC emacs-lisp
(use-package org-sticky-header
  :config
  (setq-default
   org-sticky-header-full-path 'full
   org-sticky-header-outline-path-separator " / "))
#+END_SRC

Tired of having to manually update your tables of contents?

#+BEGIN_SRC emacs-lisp
(use-package toc-org :after org)
#+END_SRC

** Python

- *TODO*: Fix for =ipython= 5.1.0.

#+BEGIN_SRC emacs-lisp
(use-package python
  :ensure nil
  :delight python-mode "Python"
  :config
  (when (executable-find "ipython")
    (setq-default
     python-shell-interpreter "ipython"
     python-shell-interpreter-args "--colors=Linux --profile=default"
     python-shell-prompt-output-regexp "Out\\[[0-9]+\\]: "
     python-shell-prompt-regexp "In \\[[0-9]+\\]: "
     python-shell-completion-setup-code
     "from IPython.core.completerlib import module_completion"
     python-shell-completion-module-string-code
     "';'.join(module_completion('''%s'''))\n"
     python-shell-completion-string-code
     "';'.join(get_ipython().Completer.all_completions('''%s'''))\n")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package pip-requirements
  :delight pip-requirements-mode "PyPA Requirements"
  :preface
  (defun me/pip-requirements-ignore-case ()
    (setq-local completion-ignore-case t))
  :init (add-hook 'pip-requirements-mode-hook #'me/pip-requirements-ignore-case))
#+END_SRC

** YAML

#+BEGIN_SRC emacs-lisp
(use-package yaml-mode
  :delight yaml-mode "YAML"
  :mode "\\.yml\\'")
#+END_SRC

* Features

** Alert

#+BEGIN_QUOTE
Alert is a Growl-workalike for Emacs which uses a common notification interface
and multiple, selectable "styles", whose use is fully customizable by the user.

[[https://github.com/jwiegley/alert][John Wiegley]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package alert
  :config
  (when (eq system-type 'darwin)
    (setq-default alert-default-style 'osx-notifier)))
#+END_SRC

** Auto-Completion

Auto-completion at point. Display a small pop-in containing the candidates.

#+BEGIN_QUOTE
Company is a text completion framework for Emacs. The name stands for "complete
anything". It uses pluggable back-ends and front-ends to retrieve and display
completion candidates.

[[http://company-mode.github.io/][Dmitry Gutov]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package company
  :defer 1
  :config
  (global-company-mode)
  (setq-default
   company-idle-delay .2
   company-minimum-prefix-length 1
   company-tooltip-align-annotations t))

(use-package company-dabbrev
  :ensure nil
  :after company
  :config (setq-default company-dabbrev-downcase nil))

(use-package company-tern
  :after company
  :config
  (add-to-list 'company-backends 'company-tern)
  (setq-default
   company-tern-meta-as-single-line t
   company-tern-property-marker " *"))
#+END_SRC

** Comments

#+BEGIN_SRC emacs-lisp
(use-package newcomment
  :ensure nil
  :bind ("<M-return>" . comment-indent-new-line)
  :config
  (setq-default
   comment-auto-fill-only-comments t
   comment-multi-line t))
#+END_SRC

** Customization Menus

This merely changes face attributes. It also /Zenburn/ customization buffers a little
more.

#+BEGIN_SRC emacs-lisp
(use-package cus-edit
  :ensure nil
  :config
  (zenburn-with-color-variables
    (set-face-attribute 'custom-group-tag nil
                        :font me/font-family
                        :height me/font-size-title)
    (set-face-attribute 'custom-state nil :foreground zenburn-green+4)))
#+END_SRC

** Dictionary

Define words using Wordnik.

#+BEGIN_SRC emacs-lisp
(use-package define-word)
#+END_SRC

** Diff

Ediff is a visual interface to Unix =diff=.

- *TODO*: Use =:bind=.

#+BEGIN_SRC emacs-lisp
(use-package ediff-util
  :ensure nil
  :preface
  (defun me/ediff-setup-keymap ()
    (ediff-setup-keymap)
    (define-key ediff-mode-map (kbd "<down>") #'ediff-next-difference)
    (define-key ediff-mode-map (kbd "<up>") #'ediff-previous-difference))
  :init (add-hook 'ediff-mode-hook #'me/ediff-setup-keymap))

(use-package ediff-wind
  :ensure nil
  :config
  (setq-default
   ediff-split-window-function #'split-window-horizontally
   ediff-window-setup-function #'ediff-setup-windows-plain))
#+END_SRC

** Dired

Configure Dired buffers. Amongst many other things, Emacs is also a file explorer.

- *TODO*: Use the new syntax for advices.

#+BEGIN_SRC emacs-lisp
(use-package dired
  :ensure nil
  :delight dired-mode "Dired"
  :init (add-hook 'dired-mode-hook #'dired-hide-details-mode)
  :config
  (defadvice dired-readin (after dired-after-updating-hook first () activate)
    "Sort dired listings with directories first before adding marks."
    (save-excursion
      (let (buffer-read-only)
        (forward-line 2)
        (sort-regexp-fields t "^.*$" "[ ]*." (point) (point-max)))
      (set-buffer-modified-p nil)))
  (setq-default
   dired-auto-revert-buffer t
   dired-hide-details-hide-symlink-targets nil
   dired-listing-switches "-alh"
   dired-ls-F-marks-symlinks nil
   dired-recursive-copies 'always))

(use-package dired-x
  :ensure nil
  :preface
  (defun me/dired-revert-after-command (command &optional output error)
    (revert-buffer))
  :config
  (advice-add 'dired-smart-shell-command :after #'me/dired-revert-after-command))
#+END_SRC

** Expand

HippieExpand manages expansions a la [[http://emmet.io/][Emmet]]. So I've gathered
all features that look anywhere close to this behavior for it to handle under the
same bind, that is =<C-return>=. Basically it's an expand DWIM.

- *TODO*: Use =:bind=.

#+BEGIN_SRC emacs-lisp
(use-package emmet-mode
  :init
  (add-hook 'css-mode-hook #'emmet-mode)
  (add-hook 'html-mode-hook #'emmet-mode)
  :config
  (setq-default emmet-move-cursor-between-quote t)
  (unbind-key "<C-return>" emmet-mode-keymap)
  (unbind-key "C-M-<left>" emmet-mode-keymap)
  (unbind-key "C-M-<right>" emmet-mode-keymap)
  (unbind-key "C-c w" emmet-mode-keymap))

(use-package hippie-exp
  :ensure nil
  :preface
  (defun me/emmet-try-expand-line (args)
    "Try `emmet-expand-line' if `emmet-mode' is active. Else, does nothing."
     (interactive "P")
     (when emmet-mode (emmet-expand-line args)))
  :bind ("<C-return>" . hippie-expand)
  :config
  (setq-default
   hippie-expand-try-functions-list '(yas-hippie-try-expand me/emmet-try-expand-line)
   hippie-expand-verbose nil))

(use-package yasnippet
  :init
  (add-hook 'emacs-lisp-mode-hook #'yas-minor-mode)
  (add-hook 'html-mode-hook #'yas-minor-mode)
  (add-hook 'js-mode-hook #'yas-minor-mode)
  (add-hook 'org-mode-hook #'yas-minor-mode)
  (add-hook 'python-mode-hook #'yas-minor-mode)
  :config
  (setq-default yas-snippet-dirs '("~/.emacs.d/snippets"))
  (yas-reload-all)
  (unbind-key "TAB" yas-minor-mode-map)
  (unbind-key "<tab>" yas-minor-mode-map))
#+END_SRC

** EXWM

- *TODO*

#+BEGIN_QUOTE
EXWM (Emacs X Window Manager) is a full-featured tiling X window manager for Emacs
built on top of [[https://github.com/ch11ng/xelb][XELB]]. It features:

- Fully keyboard-driven operations
- Hybrid layout modes (tiling & stacking)
- Dynamic workspace support
- ICCCM/EWMH compliance
- (Optional) RandR (multi-monitor) support
- (Optional) Built-in compositing manager
- (Optional) Built-in system tray
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package exwm :disabled t)
#+END_SRC

** Folding

#+BEGIN_QUOTE
This is a package to perform text folding like in Vim. It has the following features:

- folding of active regions;
- good visual feedback: it's obvious which part of text is folded;
- persistence by default: when you close file your folds don't disappear;
- persistence scales well, you can work on hundreds of files with lots of folds without
  adverse effects;
- it doesn't break indentation or something;
- folds can be toggled from folded state to unfolded and back very easily;
- quick navigation between existing folds;
- you can use mouse to unfold folds (good for beginners and not only for them);
- for fans of avy package: you can use avy to fold text with minimal number of key
  strokes!

[[https://github.com/mrkkrp/vimish-fold][Mark Karpov]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package vimish-fold
  :defer 1
  :bind
  (:map vimish-fold-folded-keymap ("<tab>" . vimish-fold-unfold)
   :map vimish-fold-unfolded-keymap ("<tab>" . vimish-fold-refold))
  :init
  (setq-default vimish-fold-dir (expand-file-name ".vimish-fold/" user-emacs-directory))
  (vimish-fold-global-mode 1)
  :config
  (setq-default vimish-fold-header-width 79))
#+END_SRC

** Help

#+BEGIN_SRC emacs-lisp
(use-package help-mode
  :ensure nil
  :bind
  (:map help-mode-map
        ("<" . help-go-back)
        (">" . help-go-forward)))
#+END_SRC

** Hydra

Hydra allows me to group binds together. It also shows a list of all implemented
commands in the eho area.

#+BEGIN_QUOTE
Once you summon the Hydra through the prefixed binding (the body + any one head), all
heads can be called in succession with only a short extension.

The Hydra is vanquished once Hercules, any binding that isn't the Hydra's head,
arrives. Note that Hercules, besides vanquishing the Hydra, will still serve his
original purpose, calling his proper command. This makes the Hydra very seamless,
it's like a minor mode that disables itself auto-magically.

[[https://github.com/abo-abo/hydra][Oleh Krehel]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package hydra
  :bind
  ("C-c <tab>" . hydra-fold/body)
  ("C-c d" . hydra-dates/body)
  ("C-c e" . hydra-eyebrowse/body)
  ("C-c f" . hydra-flycheck/body)
  ("C-c g" . hydra-magit/body)
  ("C-c o" . hydra-org/body)
  ("C-c p" . hydra-projectile/body)
  ("C-c s" . hydra-system/body)
  ("C-c w" . hydra-windows/body)
  :config (setq-default hydra-default-hint nil))
#+END_SRC

*** Dates

Group date-related commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-dates (:color blue)
  "
^
^Dates^             ^Insert^            ^Insert with time^
^─────^─────────────^──────^────────────^────────────────^──
_q_ quit            _d_ short           _D_ short
^^                  _i_ iso             _I_ iso
^^                  _l_ long            _L_ long
^^                  ^^                  ^^
"
  ("q" nil)
  ("d" me/date-short)
  ("D" me/date-short-with-time)
  ("i" me/date-iso)
  ("I" me/date-iso-with-time)
  ("l" me/date-long)
  ("L" me/date-long-with-time))
#+END_SRC

*** Eyebrowse

Group Eyebrowse commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-eyebrowse (:color blue)
  "
^
^Eyebrowse^         ^Do^                ^Switch^
^─────────^─────────^──^────────────────^──────^────────────
_q_ quit            _c_ create          _<_ previous
^^                  _k_ kill            _>_ next
^^                  _r_ rename          _e_ last
^^                  ^^                  _s_ switch
^^                  ^^                  ^^
"
  ("q" nil)
  ("<" eyebrowse-prev-window-config :color red)
  (">" eyebrowse-next-window-config :color red)
  ("c" eyebrowse-create-window-config)
  ("e" eyebrowse-last-window-config)
  ("k" eyebrowse-close-window-config :color red)
  ("r" eyebrowse-rename-window-config)
  ("s" eyebrowse-switch-to-window-config))
#+END_SRC

*** Flycheck

Group Flycheck commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-flycheck (:color blue)
  "
^
^Flycheck^          ^Errors^            ^Checker^
^────────^──────────^──────^────────────^───────^───────────
_q_ quit            _<_ previous        _?_ describe
_m_ manual          _>_ next            _d_ disable
_v_ verify setup    _f_ check           _s_ select
^^                  ^^                  ^^
"
  ("q" nil)
  ("<" flycheck-previous-error :color red)
  (">" flycheck-next-error :color red)
  ("?" flycheck-describe-checker)
  ("d" flycheck-disable-checker)
  ("f" flycheck-buffer)
  ("m" flycheck-manual)
  ("s" flycheck-select-checker)
  ("v" flycheck-verify-setup))
#+END_SRC

*** Fold

Group folding commands.

- *TODO*: Use =:bind=.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-fold (:color pink)
  "
^
^Fold^              ^Do^                ^Jump^              ^Toggle^
^────^──────────────^──^────────────────^────^──────────────^──────^────────────
_q_ quit            _f_ fold            _<_ previous        _<tab>_ current
^^                  _k_ kill            _>_ next            _S-<tab>_ all
^^                  _K_ kill all        ^^                  ^^
^^                  ^^                  ^^                  ^^
"
  ("q" nil)
  ("<tab>" vimish-fold-toggle)
  ("S-<tab>" vimish-fold-toggle-all)
  ("<" vimish-fold-previous-fold)
  (">" vimish-fold-next-fold)
  ("f" vimish-fold)
  ("k" vimish-fold-delete)
  ("K" vimish-fold-delete-all))
#+END_SRC

*** Magit

Group Magit commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-magit (:color blue)
  "
^
^Magit^             ^Do^
^─────^─────────────^──^────────────────
_q_ quit            _b_ blame
^^                  _c_ clone
^^                  _i_ init
^^                  _s_ status
^^                  ^^
"
  ("q" nil)
  ("b" magit-blame)
  ("c" magit-clone)
  ("i" magit-init)
  ("s" magit-status))
#+END_SRC

*** Org

Group Org commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-org (:color pink)
  "
^
^Org^               ^Links^             ^Outline^
^───^───────────────^─────^─────────────^───────^───────────
_q_ quit            _i_ insert          _<_ previous
^^                  _n_ next            _>_ next
^^                  _o_ open            _a_ all
^^                  _p_ previous        _v_ overview
^^                  _s_ store           ^^
^^                  ^^                  ^^
"
  ("q" nil)
  ("<" org-backward-element)
  (">" org-forward-element)
  ("a" outline-show-all)
  ("i" org-insert-link :color blue)
  ("n" org-next-link)
  ("o" org-open-at-point)
  ("p" org-previous-link)
  ("s" org-store-link)
  ("v" org-overview))
#+END_SRC

*** Projectile

Group Projectile commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-projectile (:color blue)
  "
^
^Projectile^        ^Buffers^           ^Find^              ^Search^
^──────────^────────^───────^───────────^────^──────────────^──────^────────────
_q_ quit            _b_ list            _d_ directory       _r_ replace
_i_ reset cache     _K_ kill all        _D_ root            _s_ ag
^^                  _S_ save all        _f_ file            ^^
^^                  ^^                  _p_ project         ^^
^^                  ^^                  ^^                  ^^
"
  ("q" nil)
  ("b" projectile-switch-to-buffer)
  ("d" projectile-find-dir)
  ("D" projectile-dired)
  ("f" projectile-find-file)
  ("i" projectile-invalidate-cache :color red)
  ("K" projectile-kill-buffers)
  ("p" projectile-switch-project)
  ("r" projectile-replace)
  ("s" projectile-ag)
  ("S" projectile-save-project-buffers))
#+END_SRC

*** System

Group system-related commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-system (:color blue)
  "
^
^System^            ^Processes^
^──────^────────────^─────────^─────────
_q_ quit            _p_ list
^^                  ^^
"
  ("q" nil)
  ("p" list-processes))
#+END_SRC

*** Windows

Group window-related commands.

#+BEGIN_SRC emacs-lisp
(defhydra hydra-windows (:color pink)
  "
^
^Windows^           ^Zoom^
^───────^───────────^────^──────────────
_q_ quit            _-_ zoom out
^^                  _+_ zoom in
^^                  _=_ reset
^^                  ^^
"
  ("q" nil)
  ("-" text-scale-decrease)
  ("+" text-scale-increase)
  ("=" (text-scale-increase 0)))
#+END_SRC

** IRC

- *TODO*: Display the current count of users. =(hash-table-count erc-channel-users)=

#+BEGIN_SRC emacs-lisp
(use-package erc
  :preface
  (defun me/erc ()
    "Connect to `me/erc-server' on `me/erc-port' as `me/erc-nick' with
    `me/erc-password'."
    (interactive)
    (erc :server me/erc-server
         :port me/erc-port
         :nick me/erc-nick
         :password me/erc-password))
  (defun me/erc-bol-shifted ()
    "See `erc-bol'. Support shift."
    (interactive "^")
    (erc-bol))
  (defun me/erc-set-fill-column ()
    "Set `erc-fill-column' to the width of the current window."
    (save-excursion
      (walk-windows
       (lambda (window)
         (let ((buffer (window-buffer window)))
           (set-buffer buffer)
           (when (and (erc-buffer-visible buffer) (eq major-mode 'erc-mode))
             (setq erc-fill-column (1- (window-width window)))))))))
  :bind
  (:map erc-mode-map
        ([remap erc-bol] . me/erc-bol-shifted)
        ("M-<down>" . erc-next-command)
        ("M-<up>" . erc-previous-command))
  :init
  (add-hook 'window-configuration-change-hook #'me/erc-set-fill-column)
  (me/erc)
  :config
  (make-variable-buffer-local 'erc-fill-column)
  (erc-scrolltobottom-enable)
  (setq-default
   erc-autojoin-channels-alist '(("freenode.net" "#emacs"))
   erc-fill-function 'erc-fill-static
   erc-fill-static-center 18
   erc-header-line-format " %n on %t %m"
   erc-insert-timestamp-function 'erc-insert-timestamp-left
   erc-lurker-hide-list '("JOIN" "PART" "QUIT")
   erc-prompt (format "%25s" ">")
   erc-timestamp-format "%H:%M:%S")
  (set-face-attribute 'erc-button nil :inherit 'button :weight 'unspecified)
  (zenburn-with-color-variables
    (set-face-attribute 'erc-timestamp-face nil :foreground zenburn-fg-1)))
#+END_SRC

Colorize nicks with unique colors.

- *FIXME*: Pastel'ize the colors.

#+BEGIN_SRC emacs-lisp
(use-package erc-hl-nicks :after erc)
#+END_SRC

Keep track of ERC buffers in which new messages have been posted.

#+BEGIN_SRC emacs-lisp
(use-package erc-track
  :ensure nil
  :after erc)
#+END_SRC

** Ivy, Counsel and Swiper

*** Ivy

#+BEGIN_QUOTE
Ivy is a generic completion mechanism for Emacs. While it operates similarly to
other completion schemes such as =icomplete-mode=, Ivy aims to be more
efficient, smaller, simpler, and smoother to use yet highly customizable.

[[https://github.com/abo-abo/swiper][Oleh Krehel]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package ivy
  :defer 1
  :config
  (ivy-mode 1))
#+END_SRC

*** Counsel

#+BEGIN_QUOTE
Counsel takes this further, providing versions of common Emacs commands that are
customised to make the best use of ivy. For example, =counsel-find-file= has
some additional keybindings. Pressing =DEL= will move you to the parent
directory.

[[https://github.com/abo-abo/swiper][Oleh Krehel]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package counsel
  :after ivy
  :config
  (when (eq system-type 'windows-nt)
    (setq-default counsel-ag-base-command "ag --vimgrep --nocolor --nogroup %s"))
#+END_SRC

*** Swiper

#+BEGIN_QUOTE
Swiper is an alternative to isearch that uses ivy to show an overview of all
matches.

[[https://github.com/abo-abo/swiper][Oleh Krehel]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package swiper :after ivy)
#+END_SRC

** Linters

Flycheck lints warnings and errors directly within buffers. It can check a lot of
different syntaxes, as long as you make sure that Emacs has access to the binaries
(see [[./README.md][README.md]]).

#+BEGIN_SRC emacs-lisp
(use-package flycheck
  :init
  (add-hook 'css-mode-hook #'flycheck-mode)
  (add-hook 'emacs-lisp-mode-hook #'flycheck-mode)
  (add-hook 'js-mode-hook #'flycheck-mode)
  (add-hook 'python-mode-hook #'flycheck-mode)
  :config
  (setq-default
   flycheck-check-syntax-automatically '(save mode-enabled)
   flycheck-disabled-checkers '(emacs-lisp-checkdoc)
   flycheck-display-errors-delay .3)
  (zenburn-with-color-variables
    (set-face-attribute 'flycheck-error nil :underline zenburn-red)
    (set-face-attribute 'flycheck-info nil :underline zenburn-blue+1)
    (set-face-attribute 'flycheck-warning nil :underline zenburn-orange)
    (set-face-attribute 'flycheck-fringe-error nil :foreground zenburn-red)
    (set-face-attribute 'flycheck-fringe-info nil :foreground zenburn-blue+1)
    (set-face-attribute 'flycheck-fringe-warning nil :foreground zenburn-orange)))
#+END_SRC

** Mode-Line

*** Screenshots

- *TODO*: Update screenshots and documentation.

Here is how it looks (slightly deprecated):

/In an active window/

[[./screenshots/emacs.mode-line.active.png]]

/In an inactive window/

[[./screenshots/emacs.mode-line.inactive.png]]

/When using isearch/

[[./screenshots/emacs.mode-line.search.png]]

/With an active region/

[[./screenshots/emacs.mode-line.region.png]]

- Green means buffer is clean.
- Red means buffer is modified.
- Blue means buffer is read-only.
- Colored bullets represent =flycheck= critical, warning and informational errors.
- The segment next to the clock indicate the current perspective from =eyebrowse=.

*** Delight

Delight allows you to change modes --- both major and minor --- lighters. They are
the descriptive strings than you see appear within the =mode-line=.

To make it work with =spaceline= (which uses =powerline= internally), I need to allow
it to use the newly changed strings.

- *TODO*: Use the new syntax for advices.

#+BEGIN_SRC emacs-lisp
(use-package delight
  :ensure nil
  :config
  (defadvice powerline-major-mode (around delight-powerline-major-mode activate)
    (let ((inhibit-mode-name-delight nil)) ad-do-it))
  (defadvice powerline-minor-modes (around delight-powerline-minor-modes activate)
    (let ((inhibit-mode-name-delight nil)) ad-do-it)))
#+END_SRC

*** Spaceline

Spaceline, is a mode-line configuration framework. Like what =powerline= does but at
a shallower level. It's still very customizable nonetheless.

#+BEGIN_QUOTE
This is the package that provides [[http://spacemacs.org/][Spacemacs]] with its
famous mode-line theme. It has been extracted as an independent package for general
fun and profit.

[[https://github.com/TheBB/spaceline][Eivind Fonn]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package spaceline
  :demand t
  :config

  (spaceline-define-segment me/erc-track
    "Show the ERC buffers with new messages."
    (when (bound-and-true-p erc-track-mode)
      (mapcar (lambda (buffer)
                (format "%s%s%s"
                        (buffer-name (pop buffer))
                        erc-track-showcount-string
                        (pop buffer)))
              erc-modified-channels-alist)))

  (spaceline-define-segment me/selection-info
    "Show the size of current region."
    (when mark-active
      (let* ((lines (count-lines
                     (region-beginning) (min (1+ (region-end)) (point-max))))
             (chars (- (1+ (region-end)) (region-beginning)))
             (cols (1+ (abs (- (spaceline--column-number-at-pos (region-end))
                               (spaceline--column-number-at-pos (region-beginning))))))
             (rect (bound-and-true-p rectangle-mark-mode))
             (multi-line (> lines 1)))
        (cond
         (rect (format "%d %s %d" (1- cols) (mdi "arrow-expand-all") lines))
         (multi-line (format "%d lines" (if (eq (current-column) 0) (1- lines) lines)))
         (t (format "%d chars" (1- chars)))))))

  (spaceline-define-segment me/version-control
    "Show the current version control branch."
    (when vc-mode
      (substring vc-mode (+ 2 (length (symbol-name (vc-backend buffer-file-name))))))))
#+END_SRC

- *FIXME*: Projectile segment doesn't use submodule.

#+BEGIN_SRC emacs-lisp
(use-package spaceline-config
  :ensure nil
  :after spaceline
  :config

  ;; Configure the mode-line
  (setq-default
   mode-line-format '("%e" (:eval (spaceline-ml-main)))
   powerline-default-separator 'wave
   powerline-height 20
   spaceline-highlight-face-func 'spaceline-highlight-face-modified
   spaceline-flycheck-bullet (format "%s %s" (mdi "record") "%s")
   spaceline-separator-dir-left '(left . left)
   spaceline-separator-dir-right '(right . right))

  ;; Build the mode-lines
  (spaceline-install
   `((major-mode :face highlight-face)
     ((remote-host buffer-id line) :separator ":")
     (anzu))
   `((me/selection-info)
     (me/erc-track :face spaceline-highlight-face :when active)
     ((flycheck-error flycheck-warning flycheck-info))
     ((projectile-root me/version-control) :separator (mdi "source-branch" t))
     (workspace-number)
     (global :face highlight-face)))

  ;; Customize the mode-line
  (zenburn-with-color-variables
    (set-face-attribute 'mode-line nil
                        :box nil
                        :foreground zenburn-bg+3
                        :height me/font-size-mode-line)
    (set-face-attribute 'mode-line-inactive nil
                        :box nil
                        :foreground zenburn-bg+3
                        :height me/font-size-mode-line)
    (set-face-attribute 'spaceline-flycheck-error nil :foreground zenburn-red)
    (set-face-attribute 'spaceline-flycheck-info nil :foreground zenburn-blue+1)
    (set-face-attribute 'spaceline-flycheck-warning nil :foreground zenburn-orange)
    (set-face-attribute 'spaceline-highlight-face nil
                        :background zenburn-yellow
                        :foreground zenburn-fg-1)
    (set-face-attribute 'spaceline-modified nil
                        :background zenburn-red
                        :foreground zenburn-red-4)
    (set-face-attribute 'spaceline-read-only nil
                        :background zenburn-blue+1
                        :foreground zenburn-blue-5)
    (set-face-attribute 'spaceline-unmodified nil
                        :background zenburn-green-1
                        :foreground zenburn-green+4)))
#+END_SRC

** Navigation

*** Inline

Smarter =C-a=.

#+BEGIN_SRC emacs-lisp
(global-set-key [remap move-beginning-of-line] 'me/beginning-of-line-dwim)

(defun me/beginning-of-line-dwim ()
  "Move point to first non-whitespace character, or beginning of line."
  (interactive "^")
  (let ((origin (point)))
    (beginning-of-line)
    (and (= origin (point))
         (back-to-indentation))))
#+END_SRC

*** Paragraphs

I disagree with Emacs' definition of paragraphs so I redefined the way it should jump
from one paragraph to another.

- *FIXME*: Ignore invisible text.

#+BEGIN_SRC emacs-lisp
(global-set-key [remap backward-paragraph] 'me/backward-paragraph-dwim)
(global-set-key [remap forward-paragraph] 'me/forward-paragraph-dwim)

(defun me/backward-paragraph-dwim ()
  "Move backward to start of paragraph."
  (interactive "^")
  (skip-chars-backward "\n")
  (unless (search-backward-regexp "\n[[:blank:]]*\n" nil t)
    (goto-char (point-min)))
  (skip-chars-forward "\n"))

(defun me/forward-paragraph-dwim ()
  "Move forward to start of next paragraph."
  (interactive "^")
  (skip-chars-forward "\n")
  (unless (search-forward-regexp "\n[[:blank:]]*\n" nil t)
    (goto-char (point-max)))
  (skip-chars-forward "\n"))
#+END_SRC

*** Replace

Better search and replace features. Even though I prefer to use =multiple-cursors= to
replace text in different places at once, =anzu= has a nice feedback on regexp
matches.

#+BEGIN_QUOTE
=anzu.el= is an Emacs port of [[https://github.com/osyo-manga/vim-anzu][anzu.vim]].
=anzu.el= provides a minor mode which displays /current match/ and /total matches/
information in the mode-line in various search modes.

[[https://github.com/syohex/emacs-anzu][Syohei Yoshida]]
#+END_QUOTE

/Regular replace/

[[./screencasts/emacs.anzu-replace.gif]]

/Regexp replace/

[[./screencasts/emacs.anzu-replace-regexp.gif]]

#+BEGIN_SRC emacs-lisp
(use-package anzu
  :defer 1
  :bind ([remap query-replace] . anzu-query-replace-regexp)
  :config
  (global-anzu-mode 1)
  (setq-default
   anzu-cons-mode-line-p nil
   anzu-replace-to-string-separator (format " %s " (mdi "arrow-right")))
  (zenburn-with-color-variables
    (set-face-attribute 'anzu-replace-highlight nil
                        :background zenburn-red-4
                        :foreground zenburn-red+1)
    (set-face-attribute 'anzu-replace-to nil
                        :background zenburn-green-1
                        :foreground zenburn-green+4)))
#+END_SRC

*** Scroll

Configure the mouse wheel scroll.

#+BEGIN_SRC emacs-lisp
(use-package mwheel
  :ensure nil
  :config
  (setq-default
   mouse-wheel-progressive-speed nil
   mouse-wheel-scroll-amount '(1 ((shift) . 5) ((control)))))
#+END_SRC

*** Search

#+BEGIN_QUOTE
=avy= is a GNU Emacs package for jumping to visible text using a char-based decision
tree. See also [[https://github.com/winterTTr/ace-jump-mode][ace-jump-mode]] and
[[https://github.com/Lokaltog/vim-easymotion][vim-easymotion]] - =avy= uses the same
idea.

[[https://github.com/abo-abo/avy][Oleh Krehel]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package avy
  :bind
  ([remap goto-line] . avy-goto-line)
  ([remap isearch-forward] . avy-goto-char-timer)
  :config
  (setq-default avy-background t)
  (set-face-attribute 'avy-goto-char-timer-face nil :inherit 'isearch)
  (set-face-attribute 'avy-lead-face nil :underline nil)
  (set-face-attribute 'avy-lead-face-0 nil :underline nil)
  (set-face-attribute 'avy-lead-face-1 nil :underline nil)
  (set-face-attribute 'avy-lead-face-2 nil :underline nil))
#+END_SRC

Isearch stands for /incremental search/. This means that search results are
highlighted while you are typing your query, incrementally. Since he who can do more
can do less, I've replaced default bindings with the regexp-equivalent commands.

#+BEGIN_SRC emacs-lisp
(use-package isearch
  :ensure nil
  :bind
  (("C-S-r" . isearch-backward-regexp)
   ("C-S-s" . isearch-forward-regexp)
   :map isearch-mode-map
   ("<M-down>" . isearch-ring-advance)
   ("<M-up>" . isearch-ring-retreat)
   :map minibuffer-local-isearch-map
   ("<M-down>" . next-history-element)
   ("<M-up>" . previous-history-element))
  :init
  (setq-default
   isearch-allow-scroll t
   lazy-highlight-cleanup nil
   lazy-highlight-initial-delay 0))
#+END_SRC

** OS-Specific

Augment Emacs experience for MacOS users.

#+BEGIN_SRC emacs-lisp
(when (eq system-type 'darwin)
  (setq-default
   exec-path (append exec-path '("/usr/local/bin"))  ; Add Homebrew path
   ns-command-modifier 'meta                         ; Map Meta to the Cmd key
   ns-option-modifier 'super                         ; Map Super to the Alt key
   ns-right-option-modifier nil))                    ; Disable the right Alt key
#+END_SRC

Initialize environment variables.

#+BEGIN_QUOTE
Ever find that a command works in your shell, but not in Emacs?

This happens a lot on OS X, where an Emacs instance started from the GUI inherits a
default set of environment variables.

This library works solves this problem by copying important environment variables
from the user's shell: it works by asking your shell to print out the variables of
interest, then copying them into the Emacs environment.

[[https://github.com/purcell/exec-path-from-shell][Steve Purcell]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package exec-path-from-shell
  :if (memq window-system '(mac ns))
  :defer 1
  :config (exec-path-from-shell-initialize))
#+END_SRC

** Parentheses

Highlight parenthese-like delimiters in a rainbow fashion. It ease the reading when
dealing with mismatched parentheses.

#+BEGIN_SRC emacs-lisp
(use-package rainbow-delimiters
  :init (add-hook 'prog-mode-hook #'rainbow-delimiters-mode)
  :config
  (zenburn-with-color-variables
    (set-face-attribute 'rainbow-delimiters-mismatched-face nil
                        :foreground zenburn-red-4)
    (set-face-attribute 'rainbow-delimiters-unmatched-face nil
                        :foreground zenburn-red-4)))
#+END_SRC

I am still looking for the perfect parenthesis management setup as of today... No
package seem to please my person.

- *TODO*: Find a better parenthese management package.

#+BEGIN_SRC emacs-lisp
(use-package smartparens
  :defer 1
  :bind
  (("M-<backspace>" . sp-unwrap-sexp)
   ("M-<left>" . sp-forward-barf-sexp)
   ("M-<right>" . sp-forward-slurp-sexp)
   ("M-S-<left>" . sp-backward-slurp-sexp)
   ("M-S-<right>" . sp-backward-barf-sexp))
  :config
  (require 'smartparens-config)
  (smartparens-global-mode 1)
  (sp-pair "{{" "}}")
  (sp-pair "[[" "]]"))
#+END_SRC

** Paste

#+BEGIN_SRC emacs-lisp
(use-package webpaste)
#+END_SRC

** Point and Region

Increase region by semantic units. It tries to be smart about it and adapt to the
structure of the current major mode.

#+BEGIN_SRC emacs-lisp
(use-package expand-region
  :bind
  ("C-+" . er/contract-region)
  ("C-=" . er/expand-region))
#+END_SRC

Enable multiple cursors at once. Some witchcraft at work here.

#+BEGIN_SRC emacs-lisp
(use-package multiple-cursors
  :defer 1
  :bind
  (("C-S-<mouse-1>" . mc/add-cursor-on-click)
   ("C-S-c C-S-a" . mc/vertical-align-with-space)
   ("C-S-c C-S-c" . mc/edit-lines)
   ("C-S-c C-S-l" . mc/insert-letters)
   ("C-S-c C-S-n" . mc/insert-numbers)
   ("C-'" . mc-hide-unmatched-lines-mode))
  :init
  (setq-default
   mc/list-file (expand-file-name ".multiple-cursors.el" user-emacs-directory))
  :config
  (setq-default
   mc/edit-lines-empty-lines 'ignore
   mc/insert-numbers-default 1))
#+END_SRC

Enable new custom binds when region is active. I've also added a few helpers to use
with =selected=.

#+BEGIN_SRC emacs-lisp
(use-package selected
  :defer 1
  :bind
  (:map selected-keymap
        ("<"           . mc/mark-previous-like-this)
        (">"           . mc/mark-next-like-this)
        ("C-?"         . hydra-selected/body)
        ("C-c C-c"     . me/eval-region-and-kill-mark)
        ("C-b"         . me/browse-url-and-kill-mark)
        ("C-c c"       . capitalize-region)
        ("C-c l"       . downcase-region)
        ("C-c u"       . upcase-region)
        ("C-d"         . define-word-at-point)
        ("C-f"         . fill-region)
        ("C-g"         . selected-off)
        ("C-p"         . webpaste-paste-region)
        ("C-s r"       . reverse-region)
        ("C-s s"       . sort-lines)
        ("C-s w"       . me/sort-words)
        ("<M-left>"    . me/indent-rigidly-left-and-keep-mark)
        ("<M-right>"   . me/indent-rigidly-right-and-keep-mark)
        ("<M-S-left>"  . me/indent-rigidly-left-tab-and-keep-mark)
        ("<M-S-right>" . me/indent-rigidly-right-tab-and-keep-mark))
  :config (selected-global-mode))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(defun me/eval-region-and-kill-mark (beg end)
  "Execute the region as Lisp code.
Call `eval-region' and kill mark. Move back to the beginning of the region."
  (interactive "r")
  (eval-region beg end)
  (setq deactivate-mark t)
  (goto-char beg))

(defun me/browse-url-and-kill-mark (url &rest args)
  "Ask a WWW browser to load URL.
Call `browse-url' and kill mark."
  (interactive (browse-url-interactive-arg "URL: "))
  (apply #'browse-url url args)
  (setq deactivate-mark t))

(defun me/indent-rigidly-left-and-keep-mark (beg end)
  "Indent all lines between BEG and END leftward by one space.
Call `indent-rigidly-left' and keep mark."
  (interactive "r")
  (indent-rigidly-left beg end)
  (setq deactivate-mark nil))

(defun me/indent-rigidly-right-and-keep-mark (beg end)
  "Indent all lines between BEG and END rightward by one space.
Call `indent-rigidly-right' and keep mark."
  (interactive "r")
  (indent-rigidly-right beg end)
  (setq deactivate-mark nil))

(defun me/indent-rigidly-left-tab-and-keep-mark (beg end)
  "Indent all lines between BEG and END leftward to a tab stop.
Call `indent-rigidly-left-to-tab-stop' and keep mark."
  (interactive "r")
  (indent-rigidly-left-to-tab-stop beg end)
  (setq deactivate-mark nil))

(defun me/indent-rigidly-right-tab-and-keep-mark (beg end)
  "Indent all lines between BEG and END rightward to a tab stop.
Call `indent-rigidly-right-to-tab-stop' and keep mark."
  (interactive "r")
  (indent-rigidly-right-to-tab-stop beg end)
  (setq deactivate-mark nil))

(defun me/sort-words (reverse beg end)
  "Sort words in region alphabetically, in REVERSE if negative.
Prefixed with negative \\[universal-argument], sorts in reverse.

The variable `sort-fold-case' determines whether alphabetic case
affects the sort order.

See `sort-regexp-fields'."
  (interactive "*P\nr")
  (sort-regexp-fields reverse "\\w+" "\\&" beg end))
#+END_SRC

Work on lines.

- *TODO*: Handle regions.

#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "<M-S-up>") 'me/duplicate-backward)
(global-set-key (kbd "<M-S-down>") 'me/duplicate-forward)
(global-set-key (kbd "<M-down>") 'me/swap-line-down)
(global-set-key (kbd "<M-up>") 'me/swap-line-up)

(defun me/duplicate-line (&optional stay)
  "Duplicate current line.
With optional argument STAY true, leave point where it was."
  (save-excursion
    (move-end-of-line nil)
    (save-excursion
      (insert (buffer-substring (point-at-bol) (point-at-eol))))
    (newline))
  (unless stay
    (let ((column (current-column)))
      (forward-line)
      (forward-char column))))

(defun me/duplicate-backward ()
  "Duplicate current line upward or region backward.
If region was active, keep it so that the command can be repeated."
  (interactive)
  (if (region-active-p)
      (let (deactivate-mark)
        (save-excursion
          (insert (buffer-substring (region-beginning) (region-end)))))
    (me/duplicate-line t)))

(defun me/duplicate-forward ()
  "Duplicate current line downward or region forward.
If region was active, keep it so that the command can be repeated."
  (interactive)
  (if (region-active-p)
      (let (deactivate-mark (point (point)))
        (insert (buffer-substring (region-beginning) (region-end)))
        (push-mark point))
    (me/duplicate-line)))

(defun me/swap-line-down ()
  "Move down the line under point."
  (interactive)
  (forward-line 1)
  (transpose-lines 1)
  (forward-line -1)
  (indent-according-to-mode))

(defun me/swap-line-up ()
  "Move up the line under point."
  (interactive)
  (transpose-lines 1)
  (forward-line -2)
  (indent-according-to-mode))
#+END_SRC

** Projectile

Projectile brings project-level facilities to Emacs such as grep, find and replace.

#+BEGIN_QUOTE
Projectile is a project interaction library for Emacs. Its goal is to provide a nice
set of features operating on a project level without introducing external
dependencies (when feasible). For instance - finding project files has a portable
implementation written in pure Emacs Lisp without the use of GNU find (but for
performance sake an indexing mechanism backed by external commands exists as well).

[[https://github.com/bbatsov/projectile][Bozhidar Batsov]]
#+END_QUOTE

- *TODO*: Use the new syntax for advices.

#+BEGIN_SRC emacs-lisp
(use-package projectile
  :defer 1
  :init
  (setq-default
   projectile-cache-file (expand-file-name ".projectile-cache" user-emacs-directory)
   projectile-keymap-prefix (kbd "C-c C-p")
   projectile-known-projects-file (expand-file-name
                                   ".projectile-bookmarks" user-emacs-directory))
  :config
  (projectile-global-mode)
  (setq-default
   projectile-completion-system 'ivy
   projectile-enable-caching t
   projectile-mode-line '(:eval (projectile-project-name))))
#+END_SRC

** Quality of Life

Auto-dim other buffers. Pretty self-explanatory.

- *FIXME*: Auto-dim the fringes too.
           See https://github.com/mina86/auto-dim-other-buffers.el/issues/11.

#+BEGIN_SRC emacs-lisp
(use-package auto-dim-other-buffers
  :init (auto-dim-other-buffers-mode 1)
  :config
  (zenburn-with-color-variables
    (set-face-attribute 'auto-dim-other-buffers-face nil :background zenburn-bg-05)))
#+END_SRC

Insert the current date.

#+BEGIN_SRC emacs-lisp
(defun me/date-iso ()
  "Insert the current date, ISO format, eg. 2016-12-09."
  (interactive)
  (insert (format-time-string "%F")))

(defun me/date-iso-with-time ()
  "Insert the current date, ISO format with time, eg. 2016-12-09T14:34:54+0100."
  (interactive)
  (insert (format-time-string "%FT%T%z")))

(defun me/date-long ()
  "Insert the current date, long format, eg. December 09, 2016."
  (interactive)
  (insert (format-time-string "%B %d, %Y")))

(defun me/date-long-with-time ()
  "Insert the current date, long format, eg. December 09, 2016 - 14:34."
  (interactive)
  (insert (capitalize (format-time-string "%B %d, %Y - %H:%M"))))

(defun me/date-short ()
  "Insert the current date, short format, eg. 2016.12.09."
  (interactive)
  (insert (format-time-string "%Y.%m.%d")))

(defun me/date-short-with-time ()
  "Insert the current date, short format with time, eg. 2016.12.09 14:34"
  (interactive)
  (insert (format-time-string "%Y.%m.%d %H:%M")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package delsel
  :ensure nil
  :defer 1
  :config (delete-selection-mode 1))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package eldoc
  :ensure nil
  :config (global-eldoc-mode -1))
#+END_SRC

Originally, =midnight= is used to /run something at midnight/. I use its feature that
kills old buffers.

#+BEGIN_SRC emacs-lisp
(use-package midnight
  :ensure nil
  :config
  (setq-default clean-buffer-list-delay-general 1)
  (add-to-list 'clean-buffer-list-kill-never-buffer-names "dotemacs.org"))
#+END_SRC

Augment Emacs' package menu.

#+BEGIN_QUOTE
Project for modernizing Emacs' Package Menu. With improved appearance, mode-line
information. Github integration, customizability, asynchronous upgrading, and more.

[[https://github.com/Malabarba/paradox][Artur Malabarba]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package paradox
  :config
  (setq-default
   paradox-column-width-package 27
   paradox-column-width-version 13
   paradox-execute-asynchronously t
   paradox-github-token t
   paradox-hide-wiki-packages t)
  (remove-hook 'paradox--report-buffer-print 'paradox-after-execute-functions))
#+END_SRC

Colorize colors as text with their value.

#+BEGIN_SRC emacs-lisp
(use-package rainbow-mode
  :init (add-hook 'prog-mode-hook #'rainbow-mode)
  :config (setq-default rainbow-x-colors-major-mode-list '()))
#+END_SRC

Turn on =auto-fill-mode= everywhere.

#+BEGIN_SRC emacs-lisp
(use-package simple
  :ensure nil
  :config
  (add-hook 'prog-mode-hook #'turn-on-auto-fill)
  (add-hook 'text-mode-hook #'turn-on-auto-fill))
#+END_SRC

** Slack

Slack integration.

- *TODO*
- *TODO*: Register teams in =.secret.el=.

#+BEGIN_SRC emacs-lisp
(use-package slack
  :config
  (setq-default
   slack-buffer-function 'switch-to-buffer
   slack-prefer-current-team t)
  (slack-register-team
   :name "fiftyfor"
   :default t
   :client-id me/fiftyfor-client-id
   :client-secret me/fiftyfor-client-secret
   :token me/fiftyfor-token
   :subscribed-channels '(dev general))
  (set-face-attribute 'slack-message-output-text nil :height 'unspecified)
  (zenburn-with-color-variables
    (set-face-attribute 'slack-message-output-header nil
                        :foreground zenburn-orange
                        :underline nil)))
#+END_SRC

** Version Control

Magit provides Git facilities directly from within Emacs.

#+BEGIN_QUOTE
Magit is an interface to the version control system [[https://git-scm.com/][Git]],
implemented as an [[https://www.gnu.org/software/emacs][Emacs]] package. Magit
aspires to be a complete Git porcelain. While we cannot (yet) claim that Magit wraps
and improves upon each and every Git command, it is complete enough to allow even
experienced Git users to perform almost all of their daily version control tasks
directly from within Emacs. While many fine Git clients exist, only Magit and Git
itself deserve to be called porcelains. [[https://magit.vc/about.html][(more)]]

[[https://github.com/magit/magit][Jonas Bernoulli]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package git-commit
  :preface
  (defun me/git-commit-auto-fill-everywhere ()
    (setq fill-column 72)
    (setq-local comment-auto-fill-only-comments nil))
  :init (add-hook 'git-commit-mode-hook #'me/git-commit-auto-fill-everywhere)
  :config (setq-default git-commit-summary-max-length 50))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package magit
  :preface
  (defun me/magit-display-buffer-same (buffer)
    "Display most magit popups in the current buffer."
    (display-buffer
     buffer
     (cond ((and (derived-mode-p 'magit-mode)
                 (eq (with-current-buffer buffer major-mode) 'magit-status-mode))
            nil)
           ((memq (with-current-buffer buffer major-mode)
                  '(magit-process-mode
                    magit-revision-mode
                    magit-diff-mode
                    magit-stash-mode))
            nil)
           (t '(display-buffer-same-window)))))
  :config

  ;; Use better defaults
  (setq-default
   magit-display-buffer-function 'me/magit-display-buffer-same
   magit-diff-highlight-hunk-body nil
   magit-diff-highlight-hunk-region-functions
   '(magit-diff-highlight-hunk-region-dim-outside
     magit-diff-highlight-hunk-region-using-face)
   magit-popup-display-buffer-action '((display-buffer-same-window))
   magit-refs-show-commit-count 'all
   magit-section-show-child-count t
   magit-set-upstream-on-push 'askifnotset)

  ;; Customize lighters
  (delight
   '((magit-diff-mode "Magit Diff")
     (magit-log-mode "Magit Log")
     (magit-popup-mode "Magit Popup")
     (magit-status-mode "Magit Status")))

  ;; Customize faces
  (set-face-attribute 'magit-diff-file-heading-highlight nil :background nil)
  (set-face-attribute 'magit-diff-hunk-region nil :inherit 'region)
  (set-face-attribute 'magit-popup-heading nil :height me/font-size-title)
  (set-face-attribute 'magit-section-heading nil :height me/font-size-title)
  (set-face-attribute 'magit-section-highlight nil :background nil)
  (zenburn-with-color-variables
    (set-face-attribute 'magit-diff-added nil
                        :background nil
                        :foreground zenburn-green+3)
    (set-face-attribute 'magit-diff-removed nil
                        :background nil
                        :foreground zenburn-red)))

(use-package gitattributes-mode :delight gitattributes-mode "Git Attributes")
(use-package gitconfig-mode :delight gitconfig-mode "Git Config")
(use-package gitignore-mode :delight gitignore-mode "Git Ignore")
#+END_SRC

** Whitespaces

Highlight space-like characters, eg. trailing spaces, tabs, empty lines.

#+BEGIN_SRC emacs-lisp
(use-package whitespace
  :demand t
  :ensure nil
  :init
  (add-hook 'prog-mode-hook #'whitespace-turn-on)
  (add-hook 'text-mode-hook #'whitespace-turn-on)
  :config (setq-default whitespace-style '(face empty tab trailing)))
#+END_SRC

** Windows

Allow repeated use of <kbd>←</kbd> and <kbd>→</kbd> when using =previous-buffer=
and =next-buffer=.

- *TODO*: Make a hydra.

#+BEGIN_SRC emacs-lisp
(defun me/switch-to-buffer-continue ()
  "Activate a sparse keymap:
  <left>   `previous-buffer'
  <right>  `next-buffer'"
  (set-transient-map
   (let ((map (make-sparse-keymap)))
     (define-key map (kbd "<left>") #'previous-buffer)
     (define-key map (kbd "<right>") #'next-buffer)
     map)))
(advice-add 'previous-buffer :after #'me/switch-to-buffer-continue)
(advice-add 'next-buffer :after #'me/switch-to-buffer-continue)
#+END_SRC

Save and restore Emacs status, including buffers, modes, point and windows.

#+BEGIN_SRC emacs-lisp
(use-package desktop
  :ensure nil
  :demand t
  :config
  (desktop-save-mode 1)
  (add-to-list 'desktop-globals-to-save 'golden-ratio-adjust-factor))
#+END_SRC

Workspaces within Emacs.

#+BEGIN_QUOTE
=eyebrowse= is a global minor mode for Emacs that allows you to manage your window
configurations in a simple manner, just like tiling window managers like i3wm with
their workspaces do. It displays their current state in the modeline by default. The
behaviour is modeled after [[http://ranger.nongnu.org/][ranger]], a file manager
written in Python.

[[https://github.com/wasamasa/eyebrowse][Vasilij Schneidermann]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package eyebrowse
  :defer 1
  :bind
  ("<f5>" . eyebrowse-switch-to-window-config-1)
  ("<f6>" . eyebrowse-switch-to-window-config-2)
  ("<f7>" . eyebrowse-switch-to-window-config-3)
  ("<f8>" . eyebrowse-switch-to-window-config-4)
  :config (eyebrowse-mode 1))
#+END_SRC

Adjust the size of every windows and focus the active one. It uses the mathematical
golden ratio somewhere in its formulas.

#+BEGIN_SRC emacs-lisp
(use-package golden-ratio
  :disabled t
  :demand t
  :preface
  (defconst me/golden-ratio-adjust-factor-bi-split .805)
  (defconst me/golden-ratio-adjust-factor-tri-split .53)
  (defun me/ediff-comparison-buffer-p ()
    (if (boundp 'ediff-this-buffer-ediff-sessions)
        (progn (balance-windows) ediff-this-buffer-ediff-sessions)))
  :config
  (golden-ratio-mode 1)
  (setq-default golden-ratio-adjust-factor me/golden-ratio-adjust-factor-tri-split)
  (add-to-list 'golden-ratio-exclude-modes "ediff-mode")
  (add-to-list 'golden-ratio-inhibit-functions 'me/ediff-comparison-buffer-p))
#+END_SRC

Bind commands to move around windows.

#+BEGIN_SRC emacs-lisp
(use-package windmove
  :ensure nil
  :bind
  (("C-M-<left>". windmove-left)
   ("C-M-<right>". windmove-right)
   ("C-M-<up>". windmove-up)
   ("C-M-<down>". windmove-down)))
#+END_SRC

Allow undo's and redo's with window configurations.

#+BEGIN_QUOTE
Winner mode is a global minor mode that records the changes in the window
configuration (i.e. how the frames are partitioned into windows) so that the changes
can be "undone" using the command =winner-undo=.  By default this one is bound to the
key sequence ctrl-c left.  If you change your mind (while undoing), you can press
ctrl-c right (calling =winner-redo=).

[[https://github.com/emacs-mirror/emacs/blob/master/lisp/winner.el][Ivar Rummelhoff]]
#+END_QUOTE

#+BEGIN_SRC emacs-lisp
(use-package winner
  :ensure nil
  :defer 1
  :config (winner-mode 1))
#+END_SRC

-----

[[#dotemacs][Back to top]]
